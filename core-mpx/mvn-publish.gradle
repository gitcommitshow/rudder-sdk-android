apply plugin: 'maven-publish'
apply plugin: 'signing'

task sourceJar(type: Jar) {
    classifier "sources"
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourceJar

}

signing {
    sign configurations.archives
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId 'com.rudderstack.android.sdk'
            artifactId 'core-mpx'
            version '1.0.1'

            // create the sign pom artifact
            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            artifact(sourceJar) {
                classifier = 'sources'
            }

            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")

            // create the signed artifacts
            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.*\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                    } else {
                        classifier = null
                    }
                    extension = 'jar.asc'
                }
            }
        }
    }
    repositories {
        maven {
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'Extension for Android SDK of RudderStack for Multiple Process Apps. Steer your customer data.'
            name 'RudderStack Android SDK'
            url 'https://github.com/rudderlabs/rudder-sdk-android'
            organization {
                name 'RudderStack'
                url 'https://github.com/rudderlabs'
            }
            licenses {
                license {
                    name 'MIT License'
                    url 'https://github.com/rudderlabs/rudder-sdk-android/blob/master/LICENSE'
                }
            }
            scm {
                connection 'scm:git:git://github.com/rudderlabs/rudder-sdk-android.git'
                developerConnection 'scm:git:ssh://github.com:rudderlabs/rudder-sdk-android.git'
                url 'https://github.com/rudderlabs/rudder-sdk-android/tree/fix/multiple-process-access'
            }
            developers {
                developer {
                    id 'desusai7'
                    name 'Desu Sai Venkat'
                    email 'venkat@rudderstack.com'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
    tasks.publishMavenJavaPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenJavaPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}